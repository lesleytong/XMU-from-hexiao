grammar edu.ustb.sei.mde.bxcore.dsl.BXCore with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate bXCore "http://www.ustb.edu/sei/mde/bxcore/dsl/BXCore"

BXProgram :
	imports+=ImportSection*
	javaImports=XImportSection?
	definitions += Definition*
;

ImportSection :
	'import' metamodel=[ecore::EPackage|STRING] 'as' shortName=ValidID
;

Definition :
	TypeDefinition | BXFunctionDefinition | IndexDefinition
;

TypeLiteral :
	TupleTypeLiteral | PatternTypeLiteral
;

TupleTypeLiteral :
	UnorderedTupleTypeLiteral | OrderedTupleTypeLiteral | PredefinedTypeLiteral
;

UnorderedTupleTypeLiteral :
	source=[ImportSection|ValidID] ('(' elements+=TypeVar (',' elements+=TypeVar)* ')')
;

OrderedTupleTypeLiteral :
	source=[ImportSection|ValidID] ('[' elements+=TypeVar (',' elements+=TypeVar)* ']')
;

TypeVar :
	name=ValidID ':' type=TypeRef
;

PredefinedTypeLiteral :
	type=('empty')
;

PatternTypeLiteral :
	source=[ImportSection|ValidID] root=PatternNode
;

TypeDefinition:
	('type' name=ValidID '=' literal = TypeLiteral) | PatternDefinition
;

PatternDefinition:
	'pattern' name=ValidID '=' literal=PatternTypeLiteral
;

ContextTypeRef :
	({DefinedContextTypeRef} type=[TypeDefinition|ValidID]) 
	| TupleTypeLiteral
;

BXFunctionDefinition :
	'function' name=ValidID '<' sourceType=ContextTypeRef ',' viewType=ContextTypeRef '>'  '=' statement = XmuCoreStatement
;

TypeRef :
	{EcoreTypeRef} type=[ecore::EClassifier|ValidID] |
	{FeatureTypeRef} type=[ecore::EClassifier|ValidID] (=> '::' feature=[ecore::EStructuralFeature|ValidID])
;

IndexDefinition :
	'index' name=ValidID '<' sourceType=ContextTypeRef ',' viewType=ContextTypeRef '>'
;

PatternNode :
	name=ValidID ':' type=[ecore::EClassifier|ValidID] ('{' (edges+=PatternEdge (',' edges+=PatternEdge)*)? '}')?
;

PatternEdge :
	name=ValidID ':' feature=[ecore::EStructuralFeature|ValidID] operator='=' value=PatternValueCondition
;

PatternValueCondition :
	PatternNode | PatternNodeRef
;

PatternNodeRef :
	node=[PatternNode|ValidID]
;

Pattern :
	PatternTypeLiteral | PatternDefinitionReference
;

PatternDefinitionReference :
	pattern=[PatternDefinition|ValidID]
;

XmuCoreStatement :
	XmuCoreFork |
	XmuCoreParallelComposition |
	XmuCoreCompositionChildStatement
;

XmuCoreCompositionChildStatement :
	XmuCoreMatchSource | XmuCoreMatchView |
	XmuCoreExpandSource | XmuCoreExpandView | XmuCoreGraphReplace |
	XmuCoreSwitch | XmuCoreAlign | XmuCoreFunctionCall | XmuCoreIndex
;

XmuCoreMatchSource :
	'matchS' '<' sourceType=ContextTypeRef '>' pattern=Pattern body=XmuCoreStatement
;

XmuCoreMatchView :
	'matchV' '<' viewType=ContextTypeRef '>' pattern=Pattern body=XmuCoreStatement
;

XmuCoreExpandSource :
	'expandS' pattern=Pattern '(' mappings+=VarMapping (','  mappings+=VarMapping)* ')' '->' body=XmuCoreStatement
;

XmuCoreExpandView :
	'expandV' pattern=Pattern '(' mappings+=VarMapping (','  mappings+=VarMapping)* ')' '->' body=XmuCoreStatement
;

VarMapping :
	from=ValidID '->' to=ValidID
;

XmuCoreGraphReplace :
	'replace' source=Pattern view=Pattern '{' conversions+=Conversion+ '}'
;

XmuCoreParallelComposition :
	'<' sourceType=ContextTypeRef ',' viewType=ContextTypeRef '>' bodies+=XmuCoreCompositionChildStatement (=> '|' bodies+=XmuCoreCompositionChildStatement)+ 
;

XmuCoreSwitch :
	'switch' '<' sourceType=ContextTypeRef ',' viewType=ContextTypeRef '>' '['
		branches+=XmuCoreSwitchBranch+
		adaptions+=XmuCoreSwitchAdaption*
	']'
;

XmuCoreSwitchBranch :
	'case' condition=ContextAwareCondition '->' action=XmuCoreStatement 
;

XmuCoreSwitchAdaption:
	'adaption' condition=ContextAwareCondition '->' action=ContextAwareUnidirectionalAction
;

Conversion :
	bigul=BiGULStatement '(' source+=ValidID (',' source+=ValidID)* ')' '(' view+=ValidID (',' view+=ValidID)* ')'
;

XmuCoreFork :
	'<' sourceType=ContextTypeRef ',' viewType=ContextTypeRef '>' forks+=XmuCoreForkBranch (=> '|' forks+=XmuCoreForkBranch)+
;

XmuCoreForkBranch :
	'(' sourceMappings+=VarMapping (','  sourceMappings+=VarMapping)* ')'
	'(' viewMappings+=VarMapping (','  viewMappings+=VarMapping)* ')' '->'
	body=XmuCoreStatement
;

XmuCoreAlign :
	'align' '<' sourceType=ContextTypeRef ',' viewType=ContextTypeRef '>' sourcePattern=Pattern viewPattern=Pattern 'with' alignment=ContextAwareCondition
		(('match' '->' match=XmuCoreStatement) & ('unmatchS' '->' unmatchS=ContextAwareUnidirectionalAction) & ('unmatchV' '->' unmatchV=ContextAwareUnidirectionalAction))
;

XmuCoreFunctionCall :
	'(' sourceMappings+=VarMapping (','  sourceMappings+=VarMapping)* ')'
	'(' viewMappings+=VarMapping (','  viewMappings+=VarMapping)* ')' '->' 
	target=[BXFunctionDefinition|ValidID]
;

XmuCoreIndex :
	'index' parts+=IndexPart (',' parts+=IndexPart)* 'in' body=XmuCoreStatement
;

IndexPart :
	signature=[IndexDefinition|ValidID] '<' sourceKeys+=ValidID (',' sourceKeys+=ValidID)* '|' viewKeys+=ValidID (',' viewKeys+=ValidID)* '>'
;

ContextAwareCondition :
	condition=XExpression
;

ContextAwareUnidirectionalAction :
	action=XBlockExpression
;

BiGULStatement :
	BiGULReplace | BiGULSkip
;

BiGULReplace :
	{BiGULReplace} 'replace'
;

BiGULSkip :
	{BiGULSkip} 'skip'
;

@Override
ValidID :
	ID | 'replace' | 'skip' | 'index' | 'align' | 'match' | 'int' | 'String' |'boolean' | 'type'
;